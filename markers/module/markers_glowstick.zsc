const UAS_HDLD_GLOWSTICK = "gls";

class UaS_Glowstick : UaS_Marker {
	DynamicLight GlowLight;
	// int spindir;
	int brightness;
	int age;

	Default {
		Inventory.PickupMessage "Picked up a glowstick.";
		Inventory.Icon "UGSIB0";
		tag "Glowstick";
		HDPickup.refid UAS_HDLD_GLOWSTICK;
		UaS_Marker.UseSound "UaS/Glowstick";
	}

	override void HandleAttachedLight() {
		switch(Activated) {
			case true:
				frame = 2;
				if (level.maptime % 5 == 0) { brightness = min(brightness + 1, 64); }
				if (level.maptime % 700 == 0) { age++; }
				if (!GlowLight) {
					GlowLight = DynamicLight(Actor.Spawn("UaS_GlowstickLight"));
					GlowLight.args[DynamicLight.LIGHT_RED] = 32;
					GlowLight.args[DynamicLight.LIGHT_GREEN] = 128;
					GlowLight.args[DynamicLight.LIGHT_BLUE] = 32;
				}
				GlowLight.args[DynamicLight.LIGHT_INTENSITY] = (brightness - age);
				if (owner) { GlowLight.target = owner; }
				else { GlowLight.target = self; }
				bFitsInBackpack = false;
				break;
			case false:
				frame = 1;
				if (GlowLight) { glowLight.Destroy(); }
				brightness = 0;
				age = 0;
				bFitsInBackpack = true;
				break;
		}

		// Use up when dead?
		if (Activated && (brightness - age) < 0) {
			Activated = false;
			if (GlowLight) { glowLight.Destroy(); }
			if (owner) { Amount--; }
			else { destroy(); }
		}

		//A_SetAngle(angle + vel.length() * 3 * spindir);
	}

	states {
		Spawn:
			UGSI B -1;
			stop;
	}
}

class UaS_GlowstickLight : DynamicLight {
	override void Tick() {
		super.Tick();
		if (target) {
			SetOrigin((target.pos.x, target.pos.y, target.pos.z + target.height/2), true);
			Prev = target.Prev;
		}
	}
}

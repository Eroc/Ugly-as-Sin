extend class UaS_Scarcity_Handler {
	//Remover function which counts all of an item then only removes a set percentage
	void ExactScarcity(name checkClass, double targetPercent) {

		// Build instance array for this class
		array<HDPickup> thingArray;
		HDPickup nextThing;
		let thingCounter = ThinkerIterator.Create(CheckClass);
		while(nextThing = HDPickup(thingCounter.Next(true))) {
			if (nextThing.owner) continue;
			thingArray.Push(nextThing);
		}

		// Calculate how many to keep
		int originalcount = thingArray.Size();
		if(originalcount <= 0) { return; } // sanity?
		int targetAmount = int(thingArray.Size() * targetPercent);

		// Loop over each instance until we hit the target amount
		string dbgMsg = "Scarcity processing for "..CheckClass.." x"..originalcount;
		while(thingArray.Size() > targetAmount) {
			// pick a random object from array
			int index = random(0, thingArray.Size()-1);
			inventory mo = inventory(thingArray[index]);
			if(mo.owner) {
				thingArray.Delete(index);
				continue;
			}

			//Find nearby mobs to the object
			let mobFinder = BlockThingsIterator.Create(mo, 512);
			array<HDMobBase> mobArray;
			// build a list of mobs
			while(mobFinder.Next()) {
				if(mobFinder.thing is 'HDMobBase') {
					mobArray.push(HDMobBase(mobFinder.thing));
				}
			}

			// Decide based on if mobs were found
			bool pickedUp = false;
			HDMobBase mb;
			if (mobArray.Size()>0) {
				int mobIndex = random(0, mobArray.Size()-1); // pick a random mob from the array
				mb = mobArray[mobIndex];
				if (mb && mo) {
					//Move item to mob's array
					if (mb.checkmove(mo.pos.xy,PCM_DROPOFF)) {
						mo.setorigin(mb.pos,false);
						dbgMsg = dbgMsg.."\n\t• Moved to "..mb.GetClassName();
						pickedUp = mo.CallTryPickup(mb);
					}
					else  { dbgMsg = dbgMsg.."\n\t• did not move"; }
				}
			}

			if (pickedUp) {
				//UaS.LogDebug(DBG_Scarcity, string.format("Added "..mo.getclassname().." to "..mb.getclassname()));
			}
			else {
				//UaS.LogDebug(DBG_Scarcity, string.format("Modified or removed "..mo.getclassname()));
				//DoReplacement(thingArray[index]);
				//thingArray[index].destroy();
			}
			thingArray.Delete(index);
		}

		//UaS.LogDebug(DBG_Scarcity, string.format(checkClass.." %i, %i, %i", originalcount, ThingArray.Size(), targetAmount));
		UaS.LogDebug(DBG_Scarcity, string.format(dbgMsg));
		thingArray.Clear();
	}
}

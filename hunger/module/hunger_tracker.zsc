class UaS_HungerTracker : Inventory {
	int energy;
	int hydro;
	int sick;
	int mouthtime;
	int mouth_calories;
	int mouth_fluid;
	int pending_calories;
	int pending_fluid;
	int jogged;
	bool drinking;
	transient cvar grumbles;

	default {
		+INVENTORY.PERSISTENTPOWER;
		+INVENTORY.UNTOSSABLE;
		-INVENTORY.INVBAR;
	}

	override void DoEffect() {
		if (owner.health <= 0) { return; }
		Consume();
		ProcessEnergy();
	}

	override void BeginPlay() {
		super.BeginPlay();
		// Start with a hearty meal!
		energy = 500;
		hydro = 500;
		pending_calories = 500;
		pending_fluid = 500;
	}

	void ProcessEnergy() {
		HDPlayerPawn o = HDPlayerPawn(owner);
		if (!o) { return; }
		string debugmsg = string.format("Fatigue %i, energy %i < %i, hydro %i < %i ",o.fatigue, energy, pending_calories, hydro, pending_fluid);
		string debugtype = "";
		if (o.runwalksprint >= 0) { jogged++; }
		if (o.beatcount > 0) { return; }

		bool burnedsomething = false;

		// base drain, disabled via cvar
		if (UaS_HungerMetabolism && o.beatcounter % 35 == 0) {
			energy = max(energy - 1, -100);
			hydro = max(hydro - 1, -100);
			burnedsomething = true;
			debugtype.appendformat(" \cdbase\c- ");
		}

		// Drain from jogging
		if (jogged >= 175) {
			if (o.beatcounter % 5 == 0) {
				hydro = max(hydro - 1, -100);
				jogged -= 175;
				burnedsomething = random[uas_hunger](false,true);
				debugtype.appendformat(" \ciexertion\c- ");
			}
			if (o.beatcounter % 7 == 0) {
				energy = max(energy - 1, -100);
				jogged -= 175;
				burnedsomething = random[uas_hunger](false,true);
				debugtype.appendformat(" \ciexertion\c- ");
			}
		}

		// drain from fatigue
		if (o.fatigue > 0) {
			// fatigue recovery and additional drain
			if (hydro > 0 && o.beatcounter % 4 == 0) {
				if(UaS_HungerStamina) { o.fatigue = max(o.fatigue - 1, 0); }
				hydro = max(hydro - 1, -100);
				burnedsomething = random[uas_hunger](false,true);
				debugtype.appendformat(" \cafatigue\c- ");
			}
			else if (energy > 0 && o.beatcounter % 6 == 0) {
				if(UaS_HungerStamina) { o.fatigue = max(o.fatigue - 1, 0); }
				energy = max(energy - 1, -100);
				burnedsomething = random[uas_hunger](false,true);
				debugtype.appendformat(" \cafatigue\c- ");
			}
		}

		// Do sickness
		if (burnedsomething && random[uas_hunger](0,o.fatigue) < sick) {
			pending_calories = int(pending_calories * 0.8);
			pending_fluid = int(pending_fluid * 0.6);
			if (UaS_HungerSickness) { o.fatigue = min(o.fatigue + (sick * 15), 75); }
			sick--;
			owner.A_StartSound("UaS/Gurgle",
				CHAN_AUTO, CHANF_OVERLAP,
				frandom[uas_hunger](0.9, 1.1), ATTN_STATIC,
				frandom[uas_hunger](0.8, 1.0));
			debugtype.appendformat(" \cesick %i\c- ", sick);
		}

		// Do starvation effects
		int starving = int((hydro + energy) / 10);
		if (starving < 0 && UaS_HungerStarvation) {
			o.fatigue = max(o.fatigue, abs(starving));
			debugtype.appendformat(" \c-starving %i\c- ", abs(starving));
		}

		// Process consumed food into energy
		if (pending_calories > 0 && o.beatcounter % 3 == 0) { energy++; pending_calories--; }
		if (pending_fluid > 0 && o.beatcounter % 2 == 0) { hydro++; pending_fluid--; }

		// Grumble if haven't eaten recently
		if (!grumbles) { grumbles = CVar.GetCVar("UaS_HungerGrumbles", owner.player); }
		int grumblesetting = grumbles.GetInt();
		int pend = (pending_calories + pending_fluid);
		int grumblerate = 30;
		if (energy + hydro < 0) { grumblerate = 15; }
		if (pend <= 200 && o.beatcounter % grumblerate == 0 && UaS.RandomChance(0.5)) {
			if ((grumblesetting == 1) || (grumblesetting == 0 && UaS_HungerStarvation)) {
				owner.A_StartSound("UaS/Gurgle",
					CHAN_AUTO, CHANF_OVERLAP,
					frandom[uas_hunger](0.6, 1.0), ATTN_STATIC,
					frandom[uas_hunger](1.5, 2.0));
			}
		}

		// Debuggging output
		if (o.beatcounter % 35 == 0 || debugtype != "") { UaS.LogDebug(DBG_Hunger, string.format(debugmsg..debugtype), o); }
	}

	void Consume() {
		if (mouthclear()) { return; }
		if (level.maptime % howfull() == 0) {
			string snd = "";
			if (!drinking) { snd="UaS/Chew"; }
			else if (random[uas_hunger](false, true)) { snd = "UaS/Sip"; }
			owner.A_StartSound(snd,
				CHAN_AUTO, CHANF_OVERLAP,
				frandom[uas_hunger](0.9, 1.1), ATTN_STATIC,
				frandom[uas_hunger](0.9, 1.1));
			mouthtime--;
		}
		if (mouthclear()) {
			owner.A_StartSound("UaS/Swallow",
				CHAN_VOICE, 0,
				frandom[uas_hunger](0.9, 1.1), ATTN_STATIC,
			 	frandom[uas_hunger](0.9, 1.1));
			pending_calories += mouth_calories;
			pending_fluid += mouth_fluid;
			mouth_calories = 0;
			mouth_fluid = 0;
			drinking = false;
			UaS.LogDebug(DBG_Hunger, string.format("fullness delay "..howfull()), owner);
			return;
		}
	}

	void EatOrDrink(int addcalories = 0, int addfluid = 0, int addtime = 5, int addsick = 0) {
		mouth_calories += addcalories;
		mouth_fluid += addfluid;
		mouthtime = addtime;
		sick += addsick;
	}

	bool mouthclear() {
		return (mouthtime <= 0);
	}

	int howfull() {
		int r = (pending_calories + pending_fluid) / 40;
		return clamp(r, 12, 30);
	}
}
